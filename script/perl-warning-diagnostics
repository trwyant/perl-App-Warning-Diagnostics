#!/usr/bin/env perl

use 5.006;

use strict;
use warnings;

use App::Warning::Diagnostics qw{ :all };
use Config;
use Getopt::Long 2.33 qw{ :config auto_version };
use Pod::Text;
use Pod::Usage;

our $VERSION = '0.000_017';

my %opt = (
    pager	=> -t STDOUT,
    render	=> 1,
);

my @lgl_opt = qw{ complete|completion=s exact! pager! render! };

GetOptions( \%opt,
    @lgl_opt,
    help => sub { pod2usage( { -verbose => 2 } ) },
) or pod2usage( { -verbose => 0 } );

if ( $opt{complete} ) {
    my $code = __PACKAGE__->can( "complete_$opt{complete}" )
	or die "Completion style '$opt{complete}' not defined\n";
    defined $ENV{COMP_LINE}	# For testing
	or local $ENV{COMP_LINE} = "$0 @ARGV";
    $code->( @lgl_opt, qw{ help version } );
    exit;
}

my $code = __PACKAGE__->can( $opt{exact} ?
    'warning_diagnostics_exact' :
    'warning_diagnostics'
);
defined( my $raw_pod = $code->( @ARGV ) )
    or die "No warning categories specified\n";

my $out;
if ( $opt{pager} ) {
    my $pager = $ENV{PAGER} || $Config{pager};
    open $out, '|-', $pager	## no critic (RequireBriefOpen)
	or warn "Failed to open pipe to $pager: $!\n";
}

$out ||= \*STDOUT;

if ( defined( my $enc = pod_encoding() ) ) {
    binmode $out, ":encoding($enc)"
	or die "Unable to specify encoding $enc for output: $!\n";
}

if ( $opt{render} ) {
    my $psr = Pod::Text->new();
    my $fh;
    if ( "$]" >= 5.008 ) {
	my $scalar_ref = \$raw_pod;	# To fool Perl::MinimumVersion
	open $fh, '<', $scalar_ref	## no critic (RequireBriefOpen)
	    or die "Failed to open SCALAR reference: $!\n";
    } else {
	require IO::String;
	$fh = IO::String->new( $raw_pod );
    }
    $psr->parse_from_filehandle( $fh, $out );
} else {
    print { $out } $raw_pod;
}

sub complete_bash {
    my @option = @_;

    if ( ( scalar completion_words( $ENV{COMP_LINE},
		$ENV{COMP_POINT} ) ) =~ m/ [^:] : \z /smx ) {
	print ":\n";
	return;
    }

    my @rslt = complete_zsh_bash( @option );

    while ( @rslt == grep { m/ :: /smx } @rslt ) {
	s/ \A [^:]+ :: //smx for @rslt;
    }

    s/ (?<= :: ) .* //smx for @rslt;

    {
	my %uniq;
	@rslt = grep { ! $uniq{$_}++ } @rslt;
    }

    local $\ = @rslt == 1 ? " \n" : "\n";
    print for @rslt;
    return;
}

sub complete_zsh_bash {
    my @option = @_;

    my $line = $ENV{COMP_LINE};
    my $point = $ENV{COMP_POINT};
    defined $point
	or $point = length $line;

    my @rslt = complete( $line, $point, @option );
    wantarray
	and return @rslt;
    local $\ = "\n";
    print for @rslt;
    return;
}

__END__

=head1 TITLE

perl-warning-diagnostics - List diagnostics enabled by specified warning categories

=head1 SYNOPSIS

 perl-warning-diagnostics uninitialized
 perl-warning-diagnostics io no-unopened no-closed
 perl-warning-diagnostics --help
 perl-warning-diagnostics --version

=head1 OPTIONS

=head2 --complete

 --complete bash
 --complete zsh_bash

This option specifies that command completion be done in the specified
style. Valid styles are:

=over

=item bash

Bash completion. This is set up in F<.bashrc> as

 complete -o nospace -C 'perl-warning-diagnostics --complete bash' \
     script/perl-warning-diagnostics

=item zsh_bash

Zsh emulation of Bash completion. This is set up in F<bashcompinit> as

 complete -C 'perl-warning-diagnostics --complete zsh_bash' \
     perl-warning-diagnostics

=back

For completion under Bash specify

 complete \
   -C 'perl-warning-diagnostics --complete bash' \
   perl-warning-diagnostics

in F<~/.bashrc>. For completion under zsh Bash completion emulation
specify

 complete \
   -C 'perl-warning-diagnostics --complete zsh_bash' \
   perl-warning-diagnostics

in F<~/.zshrc>.

=head2 --completion

This is just an alias for L<--complete|/--complete>.

=head2 --exact

If this Boolean option is asserted, subcategories of group categories
are B<not> included in the output.

=head2 --help

This option displays the documentation for this script. The script then
exits.

=head2 --pager

If this Boolean option is asserted, the output is routed to a pager,
either the pager specified by environment variable C<PAGER>, or to the
pager specified in configuration variable C<pager> if the environment
variable is not defined.

The default is C<--pager> if C<STDOUT> is a terminal; otherwise it is
C<--no-pager>.

=head2 --render

If this Boolean option is asserted the POD is rendered using
L<Pod::Text|Pod::Text>. Otherwise the raw POD is output.

The default is C<--render>, but you can disable this by specifying
C<--no-render>.

=head2 --version

This option displays the version of this script. The script then exits.

=head1 DETAILS

This Perl script displays the diagnostics that are enabled by enabling
the warnings specified on the command line. More than one warning
category can be specified, and categories can be negated by prefixing
C<'no'> or C<'no-'> to their names. Categories are combined in
left-to-right order, so specifying arguments

 io noclosed no-unopened

specifies the diagnostics possible under

 no warnings;
 use warnings qw{ io };
 no warnings qw{ closed unopened };

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2021-2022, 2025 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
